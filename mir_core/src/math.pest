WHITESPACE = _{ " " | "\t" | "\r\n" | "\n" }

number  = @{ (integer ~ ("_"+ ~ integer)* ~ ("." ~ ASCII_DIGIT+)? | !integer ~ "." ~ ASCII_DIGIT+) ~ (^"e" ~ integer)? }
integer = _{ ("+" | "-")? ~ ASCII_DIGIT+ }

bool = { "true" | "false" }

null = { "null" }

string_value = @{ (!"\"" ~ ANY)* }
string       = ${ "\"" ~ string_value ~ "\"" }

lambda        = ${ argument_list ~ WHITESPACE* ~ "=>" ~ WHITESPACE* ~ expression }
argument_list = !{ identifier | "(" ~ (identifier ~ ("," ~ identifier)*)? ~ ")" }

identifier      = @{ ASCII_ALPHA+ ~ identifier_rest* }
identifier_rest = _{ ASCII_ALPHA+ | ASCII_DIGIT+ | "_"+ }

infix_op        = _{ add | subtract | multiply | divide | modulo | power | equal | not_equal | less_eq | less | greater_eq | greater | each_equal | each_not_equal | each_less_eq | each_less | each_greater_eq | each_greater | and | or }
add             =  { "+" }
subtract        =  { "-" }
multiply        =  { "*" }
divide          =  { "/" }
modulo          =  { "%" }
power           =  { "^" }
equal           =  { "==" }
not_equal       =  { "!=" }
less_eq         =  { "<=" }
less            =  { "<" }
greater_eq      =  { ">=" }
greater         =  { ">" }
each_equal      =  { "[==]" }
each_not_equal  =  { "[!=]" }
each_less_eq    =  { "[<=]" }
each_less       =  { "[<]" }
each_greater_eq =  { "[>=]" }
each_greater    =  { "[>]" }
and             =  { "and" | "&&" }
or              =  { "or" | "||" }

prefix_op = _{ negation | invert }
negation  =  { "-" }
invert    =  { "!" }

postfix_op = _{ factorial }
factorial  =  { "!" }

spread_operator       =  { "..." }
spread_expression     = ${ spread_operator ~ expression }
spreadable_expression = _{ spread_expression | expression }

call_list     = !{ "(" ~ (spreadable_expression ~ ("," ~ spreadable_expression)*)? ~ ")" }
function_call = ${ identifier ~ call_list }

list        = !{ "[]" | "[" ~ spreadable_expression ~ ("," ~ spreadable_expression)* ~ "]" }
list_access = ${ identifier ~ "[" ~ WHITESPACE* ~ expression ~ WHITESPACE* ~ "]" }

conditional = ${ "if" ~ WHITESPACE+ ~ expression ~ WHITESPACE+ ~ "then" ~ WHITESPACE+ ~ expression ~ WHITESPACE+ ~ "else" ~ WHITESPACE+ ~ expression }

expression        = ${ prefix_op* ~ term ~ postfix_op* ~ (WHITESPACE* ~ infix_op ~ WHITESPACE* ~ prefix_op* ~ term ~ postfix_op*)* }
nested_expression = _{ "(" ~ WHITESPACE* ~ expression ~ WHITESPACE* ~ ")" }
term              = _{ conditional | function_call | lambda | assignment | list | list_access | bool | string | null | identifier | number | nested_expression }

assignment = !{ identifier ~ "=" ~ expression }

comment = { "//" ~ (!"\n" ~ ANY)* }

input = _{ SOI ~ (expression | comment) ~ comment? ~ EOI }
