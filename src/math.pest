WHITESPACE = _{ " " | "\t" | "\r\n" | "\n" }

number  = @{ integer ~ ("." ~ ASCII_DIGIT+)? ~ (^"e" ~ integer)? }
integer = _{ ("+" | "-")? ~ ASCII_DIGIT+ }

identifier      = @{ !command ~ ASCII_ALPHA+ ~ identifier_rest* }
identifier_rest = _{ ASCII_ALPHA+ | ASCII_DIGIT+ | "_"+ }

infix_op = _{ add | subtract | multiply | divide | modulo | power }
add      =  { "+" }
subtract =  { "-" }
multiply =  { "*" }
divide   =  { "/" }
modulo   =  { "%" }
power    =  { "^" }

prefix_op = _{ negation }
negation  =  { "-" }

postfix_op = _{ factorial }
factorial  =  { "!" }

expression        =  { prefix_op* ~ term ~ postfix_op* ~ (infix_op ~ prefix_op* ~ term ~ postfix_op*)* }
nested_expression = _{ "(" ~ expression ~ ")" }
term              = _{ identifier | number | nested_expression }

assignment = { identifier ~ "=" ~ expression }

command = { quit | history | help }
quit    = { "quit" }
history = { "history" }
help    = { "help" }

calculation = _{ SOI ~ (command | assignment | expression) ~ EOI }
